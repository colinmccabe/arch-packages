# Modified from https://aur.archlinux.org/packages/x11docker/

_name=x11docker
pkgname=$_name
pkgver=6.9.0
_mainfolder=$pkgname-$pkgver
pkgrel=1
pkgdesc='Run GUI applications and desktops in Docker. Focus on security.'
arch=(any)
url=https://github.com/mviereck/x11docker
license=(MIT)
install=x11docker.install

# https://github.com/mviereck/x11docker/wiki/dependencies
depends=(docker nxagent xorg-xinit xorg-xauth xclip
         xorg-xhost xorg-xrandr xorg-xdpyinfo)

source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
        pulse-auth-anonymous.patch
        x11docker.install)
sha256sums=('2206866d289e7f0e0fa710af3a62bd0afb1568f7b5510efcf1c2fd5c4f5082c8'
            'c71b0def02506e450f6c9359cb2a3975b793672d14fa522f49cd1d722919e037'
            '0a3cc92a7530d753381edc04ca86afbc28019678bef1168c3bb0183b814c3714')

prepare() {
    cd "$srcdir"
    patch --forward --strip=1 --input="${srcdir}/pulse-auth-anonymous.patch"
}

package() {
  cd "$srcdir/$_mainfolder"

  install -Dm755 x11docker              -t "$pkgdir/usr/bin"
  install -Dm644 README.md CHANGELOG.md -t "$pkgdir/usr/share/doc/$_name"
  install -Dm644 LICENSE.txt            -t "$pkgdir/usr/share/licenses/$_name"
}
