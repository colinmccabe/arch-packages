# Modified from https://aur.archlinux.org/packages/x11docker/

_name=x11docker
pkgname=$_name
pkgver=6.6.2
_mainfolder=$pkgname-$pkgver
pkgrel=1
pkgdesc='Run GUI applications and desktops in Docker. Focus on security.'
arch=(any)
url=https://github.com/mviereck/x11docker
license=(MIT)
install=x11docker.install

# https://github.com/mviereck/x11docker/wiki/dependencies
depends=(docker nxagent xorg-xinit xorg-xauth xclip
         xorg-xhost xorg-xrandr xorg-xdpyinfo)

source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
        pulse-auth-anonymous.patch
        x11docker.install)
sha256sums=('cae2a5f45679a650ed6bff37b27f1cb00999e35f05d159f98165c53ebf1a8b1b'
            'd1024b7a7c1d6f88e2d0594fe2e8e4dbf63d3598d02e0b3ae7f75c4cf957697c'
            '0a3cc92a7530d753381edc04ca86afbc28019678bef1168c3bb0183b814c3714')

prepare() {
    cd "$srcdir"
    patch --forward --strip=1 --input="${srcdir}/pulse-auth-anonymous.patch"
}

package() {
  cd "$srcdir/$_mainfolder"

  install -Dm755 x11docker              -t "$pkgdir/usr/bin"
  install -Dm644 README.md CHANGELOG.md -t "$pkgdir/usr/share/doc/$_name"
  install -Dm644 LICENSE.txt            -t "$pkgdir/usr/share/licenses/$_name"
}
